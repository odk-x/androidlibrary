/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

libraryScripts.each {
    apply from: it
}

apply plugin: 'com.android.library'
apply plugin: "com.jfrog.artifactory"
apply plugin: "maven-publish"

group = groupId

android {

    // The following are set in the common.gradle file
    compileSdkVersion(compileVersion)
    buildToolsVersion(buildTools)
    defaultConfig {
        minSdkVersion(minVersion)
        targetSdkVersion(targetVersion)
        versionCode(releaseVersionCode)
        versionName(versionCodeName)
        testInstrumentationRunner(instrumentationRunner)
    }

    // set by command line arguments
    defaultPublishConfig(gradle.ext.odkDefaultPublishConfig)

    flavorDimensions "stage", "testing"

    productFlavors {
        demo {
            dimension 'stage'
        }

        master {
            dimension 'stage'
        }

        snapshot {
            dimension 'stage'
        }

        basic {
            dimension 'testing'
        }

        uitest {
            minSdkVersion(uitestMinSDK)
            dimension 'testing'
        }
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            matchingFallbacks = ['debug']
        }
        debug {
            debuggable(true)
            testCoverageEnabled(testCoverage) // Allows AndroidTest JaCoCo reports to be generated
            matchingFallbacks = ['release']
        }
    }

    packagingOptions { // Pick first occurrence of any files that cause conflicts, as defined in common.gradle
        packageList.each {
            pickFirst it
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
}


configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}


dependencies {
    api fileTree(include: '*.jar', dir: 'libs')
    api 'net.jcip:jcip-annotations:1.0'
    api 'androidx.annotation:annotation:1.1.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.appcompat:appcompat:1.0.2'
    api 'androidx.preference:preference:1.0.0'
    api 'androidx.fragment:fragment:1.2.0-alpha01'
    api 'commons-io:commons-io:2.6'
    api 'org.apache.commons:commons-lang3:3.5'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    api 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    api 'com.fasterxml.jackson.core:jackson-databind:2.9.9.2'
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.9'
    api 'joda-time:joda-time:2.10.3'

    // Testing dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'

}

publishing {
    publications {
        aarRelease(MavenPublication) {}
        aarDebug(MavenPublication) {}
    }
    repositories {
        ivy {
            ivyPattern('[organisation]/[module]/[revision]/ivy-[revision].xml')
            artifactPattern('[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]')
        }
    }
}

task updateClassifierSnapshotBasic {
    doLast {
        bundleSnapshotBasicReleaseAar.classifier snapshotRelease
        bundleSnapshotBasicDebugAar.classifier snapshotDebug

        publishing.publications.aarRelease.artifact bundleSnapshotBasicReleaseAar
        publishing.publications.aarDebug.artifact bundleSnapshotBasicDebugAar

        project.getArtifacts().add("default", bundleSnapshotBasicDebugAar)
    }
}

task updateClassifierDemoBasic {
    doLast {
        bundleDemoBasicReleaseAar.classifier demoRelease
        bundleDemoBasicDebugAar.classifier demoDebug

        publishing.publications.aarRelease.artifact bundleDemoBasicReleaseAar
        publishing.publications.aarDebug.artifact bundleDemoBasicDebugAar

        project.getArtifacts().add("default", bundleDemoBasicDebugAar)
    }
}


task spawnSnapshotBasicArchives(dependsOn: ['snapshotVersion', 'assembleSnapshotBasicDebug',
                                            'assembleSnapshotBasicRelease', 'updateClassifierSnapshotBasic'])

task spawnDemoBasicArchives(dependsOn: ['snapshotVersion', 'assembleDemoBasicDebug',
                                            'assembleDemoBasicRelease', 'updateClassifierDemoBasic'])