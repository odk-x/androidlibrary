/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

libraryScripts.each {
    apply from: it
}

apply plugin: 'com.android.library'
apply plugin: "maven-publish"


android {
    namespace(libraryNamespace)
    compileSdk(compileVersion)
    defaultConfig {
        minSdkVersion(minVersion)
        targetSdkVersion(targetVersion)
        testInstrumentationRunner(instrumentationRunner)
    }

    buildFeatures {
        aidl true
        buildConfig true
    }

    flavorDimensions.add("stage")

    productFlavors {
        primary {
            dimension 'stage'
            versionCode(primaryVersionCode)
            versionName(primaryVersion)
        }

        snapshot {
            dimension 'stage'
            versionCode(snapshotVersionCode)
            versionName(snapshotVersion)
        }
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            matchingFallbacks = ['debug']
        }
        debug {
            debuggable(true)
            matchingFallbacks = ['release']
        }
    }


    packagingOptions {
        exclude 'META-INF/*'
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    publishing {
        singleVariant("snapshotDebug")
        singleVariant("primaryRelease")
    }
}


configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}


dependencies {
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'org.opendatakit:odkx-rest-interface:2.2.0-SNAPSHOT'
    implementation 'net.jcip:jcip-annotations:1.0'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.fragment:fragment:1.5.7'

    // Testing dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.annotation:annotation:1.7.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "ODKXAndroidLibary"
                url = "https://maven.pkg.github.com/odk-x/androidlibrary"
                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
        publications {
            snapshotDebug(MavenPublication) {
                from components.snapshotDebug
                groupId = odkxGroupId
                artifactId = libraryPackageName
                version = snapshotVersion
            }
            primaryRelease(MavenPublication) {
                from components.primaryRelease
                groupId = odkxGroupId
                artifactId = libraryPackageName
                version = primaryVersion
            }
        }
    }
}
